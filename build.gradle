apply from: 'dependencies.gradle'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.1'
    }
}

allprojects {
    group = 'com.hotmart.barcodescanner'
    version = '1.0'

    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/groups/public'
        }
    }

    ext {
        isLibrary = false
    }
}

subprojects {
    afterEvaluate { Project project ->
        ext.pluginContainer = project.getPlugins()
        if(ext.pluginContainer.hasPlugin("com.android.application") || ext.pluginContainer.hasPlugin("com.android.library")) {
            android {
                compileSdkVersion versions.compile_sdk
                buildToolsVersion versions.build_tools

                android {
                    lintOptions {
                        abortOnError false
                    }
                }

                defaultConfig {
                    minSdkVersion versions.min_sdk
                    targetSdkVersion versions.target_sdk
                    versionCode 1
                    versionName "1.0"
                }
            }
        }
        if(project.isLibrary && rootProject.hasProperty('sonatypeUsername') && rootProject.hasProperty('sonatypePassword')) {
            configure(project) {
                apply plugin: 'maven'

                uploadArchives {
                    repositories.mavenDeployer {

                        repository(url: sonatypeRepo) {
                            authentication(userName: sonatypeUsername , password: sonatypePassword)
                            releases(updatePolicy: 'always')
                        }

                        pom.project {
                            name project.pomName
                            version project.version
                            groupId project.group
                            packaging project.pomPackaging
                            artifactId project.pomArtifactId
                            description project.pomDescription
                            url 'https://github.com/Hotmart-Org/barcodescanner'

                            scm {
                                url 'git@github.com:Hotmart-Org/barcodescanner.git'
                                connection 'git@github.com:Hotmart-Org/barcodescanner.git'
                                developerConnection 'git@github.com:Hotmart-Org/barcodescanner.git'
                            }

                            licenses {
                                license {
                                    name 'The Apache Software License, Version 2.0'
                                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                    distribution 'repo'
                                }
                            }

                            developers {
                                developer {
                                    id 'joaonh'
                                    name 'Jo√£o Nogueira'
                                    email 'joao.nogueira@hotmart.com.br'
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
}
